<!DOCTYPE html>
<html>

  <head>

    <meta charset="utf-8">
    
    <title> Rayado </title>
    
    <style type="text/css">
      canvas {border: 1px solid black; background:black}
    </style>

    <script id="myVertexShader" type="x-shader/x-vertex">#version 300 es
      in       vec3  VertexPosition;
      in       vec3  VertexNormal;
      in       vec2  VertexTexcoords;
      out      vec3  N, ec;
      out      float texCoordS;
      uniform  mat4  projectionMatrix;
      uniform  mat4  modelViewMatrix;
      uniform  mat3  normalMatrix;
      
      void main()  {
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
        gl_Position     = projectionMatrix * ecPosition;
        texCoordS       = VertexTexcoords.s;
      }
    </script>
    
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
      
      precision mediump float;

      in  vec3  N, ec;
      in  float texCoordS;
      out vec4  fragmentColor;
      
      uniform vec3  StripeColor;  // color de la raya
      uniform float StripeScale;  // numero de rayas
      uniform float StripeWidth;  // ancho de la raya
      
      struct LightData {
        vec3 La;         // Ambiente
        vec3 Ld;         // Difusa
        vec3 Ls;         // Especular
        vec3 Lp;         // Posición en coordenadas del ojo
      };
      uniform LightData Light;
      
      struct MaterialData {
        vec3 Ka;         // Ambiente
        vec3 Kd;         // Difusa
        vec3 Ks;         // Especular
        float shininess; // Exponente para el Brillo
      };
      uniform MaterialData Material;
        
      vec3 phong (vec3 N, vec3 L, vec3 V, vec3 newKd) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          
          vec3  R          = reflect(-L, N);;
          float RdotV_n    = pow(max(0.0, dot(R,V)), Material.shininess);
          
          diffuse  = NdotL   * (Light.Ld * newKd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
          
        }
        return (ambient + diffuse + specular);
      }

      void main() {
        
        vec3 n = normalize(N);
        vec3 L = normalize(Light.Lp - ec);
        vec3 V = normalize(-ec);
          
        if (gl_FrontFacing == false) n = -n;

        float scaled = fract (texCoordS * StripeScale);
        float s      = step  (StripeWidth, scaled);
        vec3  newKd  = mix   (StripeColor, Material.Kd, s);
          
        fragmentColor = vec4 (phong (n, L, V, newKd), 1.0);
          
      }
      
    </script>

  </head>
  
  <body>

    <button>Plano</button> 
    <button>Cubo</button> 
    <button>Tapa</button> 
    <button>Cono</button> 
    <button>Cilindro</button> 
    <button>Esfera</button> 
    <br>
    <input type="color" id="StripeColor" name="color" value="#ffffff"> &nbsp;Color del Rayado<br>
    <input type="range" id="StripeWidth" name="range" min="0" max="100" value="0"> &nbsp;Ancho de la Raya<br>
    <input type="range" id="StripeScale" name="range" min="1" max="30" value="1"> &nbsp;Factor de Repetición<br>

    <canvas id="myCanvas" width="600" height="600">
      El Navegador no soporta HTML5
    </canvas>
    
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
    <script src="comun.js"></script>
    <script src="rayado.js"></script>

  </body>

</html>
