<!DOCTYPE html>
<html>

  <head>
  
    <meta charset="utf-8">
    
    <title> Vertex displacement </title>
    
    <style>
      canvas {border: 1px solid black;}
    </style>
    
    <script id="myVertexShader" 
          type="x-shader/x-vertex">#version 300 es
        
      uniform mat4 projectionMatrix;
      uniform mat4 modelViewMatrix;
      uniform mat3 normalMatrix;

      uniform float Time;
      uniform float K;         // wavenumber
      uniform float Velocity;  // velocidad
      uniform float Amp;       // amplitud

      in vec3 VertexPosition;
        
      out vec3 N, ec;

      void main()  {
          
        vec4 pos = vec4(VertexPosition,1.0);
        float u = K * (pos.x - Velocity * Time);
        pos.z = Amp *sin(u);
          
        vec3 n = vec3(0.0);
        n.xz = normalize(vec2(-K*Amp*cos(u),1.0));
          
        vec4 ecPosition= modelViewMatrix * pos;
        N  = normalize(normalMatrix * n);
          
        ec = vec3(ecPosition);
          
        gl_Position = projectionMatrix * ecPosition;

      }
      
    </script>
      
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
        
      precision mediump float;
        
      struct LightData {
        vec3 Position; 
        vec3 La;       
        vec3 Ld;       
        vec3 Ls;       
      };
      uniform LightData Light;
      
      struct MaterialData {
        vec3 Ka;       
        vec3 Kd;       
        vec3 Ks;       
        float alpha;   
      };
      uniform MaterialData Material;
        
      in  vec3 N, ec;
      out vec4 fragmentColor;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
      
        return (ambient + diffuse + specular);
      }
      
      void main() {
        
        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
          
        fragmentColor = vec4( phong(n,L,V) * 1.6, 1.0);
        
      }
      
    </script>

  </head>
  
  <body>
  
    <input type="range" name="K" min="0" max="100" value="100">&nbsp;K<br>
    <input type="range" name="Velocity" min="0" max="10" value="5">&nbsp;Velocity<br>
    <input type="range" name="Amp" min="1" max="100" value="10">&nbsp;Amp<br>
      
    <canvas id="myCanvas" width="600" height="600">
      El navegador no soporta HTML5
    </canvas>
          
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
    <script src="comun.js"></script>
    <script src="examplePlaneTeselado.js"></script>
    <script src="vd.js"></script>
    
  </body>
  
</html>
