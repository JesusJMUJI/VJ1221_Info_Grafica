<!DOCTYPE html>
<html>

  <head>
  
    <meta charset="utf-8">
    
    <title> Postproceso </title>
    
    <style>
      canvas {border: 1px solid black;}
    </style>
    
    <script id="myVertexShaderFBO" 
          type="x-shader/x-vertex">#version 300 es
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
      
      in vec3 VertexPosition;
      in vec3 VertexNormal;
      
      out vec3 N, ec;
  
      void main()  {
      
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
        
        gl_Position     = projectionMatrix * ecPosition;
      
      }
      
    </script>
    
    <script id="myFragmentShaderFBO" 
          type="x-shader/x-fragment">#version 300 es
      
      precision highp float;
      
      struct LightData {
        vec3 Position;
        vec3 La;
        vec3 Ld;
        vec3 Ls;
      };
      uniform LightData Light;
    
      struct MaterialData {
        vec3 Ka;
        vec3 Kd;
        vec3 Ks;
        float alpha;
      };
      uniform MaterialData Material;

      uniform float Gamma;
      in  vec3  N, ec;
      out vec4 fragmentColor;

      vec3 phong (vec3 N, vec3 L, vec3 V) {
      
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
      }

      void main() {

        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
      
        vec3 myColor = phong(n,L,V);

        fragmentColor = vec4(myColor,1.0);
      
      }
      
    </script>
    
    <script id="myVertexShader" 
          type="x-shader/x-vertex">#version 300 es
      
      in vec3 VertexPosition;
      in vec2 VertexTexcoords;

      out vec2 texCoords;

      void main()  {
      
        gl_Position = vec4(VertexPosition, 1.0);
        texCoords   = VertexTexcoords;

      }
      
    </script>
    
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
      
      precision highp float;
      
      uniform sampler2D myTexture;
      uniform float Brightness;
      uniform float Saturation;
      uniform float Contrast;
      
      in  vec2 texCoords;
      out vec4 fragmentColor;

      void main() {
        
        // color de la textura
        vec3 color     = vec3(texture(myTexture,texCoords));
        
        // calculo de la saturacion
        vec3 LumCoeff  = vec3(0.2125, 0.7154, 0.0721);
        vec3 AvgLumin  = vec3(0.5, 0.5, 0.5);
        
        vec3 intensity = vec3(dot(color, LumCoeff));
        vec3 satColor  = mix(intensity, color, Saturation);
        vec3 conColor  = mix(AvgLumin, satColor, Contrast);
        
        fragmentColor =  vec4(conColor * Brightness, 1.0);
      
      }
      
    </script>
    
  </head>
    
  <body>
  
    <input type="range" name="Brightness" min="0" max="100" value="25">&nbsp;Brightness<br>
    <input type="range" name="Saturation" min="0" max="100" value="50">&nbsp;Saturation<br>
    <input type="range" name="Contrast"   min="0" max="100" value="50">&nbsp;Contrast<br>
   
    <canvas id="myCanvas" width="600" height="600">
      El navegador no soporta HTML5
    </canvas>
    
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
    <script src="comun.js"></script>
    <script src="postproceso.js"></script>

  </body>

</html>
